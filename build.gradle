buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.6"
}
dokka {
    kotlinTasks {
        defaultKotlinTasks() +
                [project(":sandwiches").compileKotlin,
                project(":util").compileKotlin,
                project(":stores").compileKotlin,
                project(":app").compileKotlin]
    }
    jdkVersion = 8
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
    includeNonPublic = true
    includes = ['modules.md', 'packages.md']
}

task clean {
    doFirst {
        delete "$buildDir"
    }
    subprojects.each {
        it.afterEvaluate {
            def cleanTask = it.tasks.findByName('clean')
            if (cleanTask) {
                dependsOn(cleanTask)
            }
        }
    }
}