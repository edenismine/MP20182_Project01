buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
    dokka {
        kotlinTasks {
            def subdeps = project.configurations
                    .collectMany { it.allDependencies }
                    .findAll { it instanceof ProjectDependency }
                    .toSet()
            def deps = subdeps.collect{":$it.name:compileKotlin"}
            def compileKotlinStrings = new ArrayList<String>()
            deps.each {compileKotlinStrings.add(it.toString())}
            defaultKotlinTasks() + compileKotlinStrings
        }
        jdkVersion = 8
        outputFormat = 'html'
        outputDirectory = "$rootProject.projectDir/docs"
        includeNonPublic = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.7"
}


task clean {
    doFirst {
        delete "$buildDir"
        delete "$projectDir/docs"
    }
    subprojects.each {
        it.afterEvaluate {
            def cleanTask = it.tasks.findByName('clean')
            if (cleanTask) {
                dependsOn(cleanTask)
            }
        }
    }
}