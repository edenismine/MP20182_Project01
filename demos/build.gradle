apply plugin: 'application'

dependencies {
    compile project(":stores")
    compile project(":util")
    compile project(":sandwiches")
}

def includeFiles = project.configurations.collectMany {it.allDependencies}
        .findAll {it instanceof ProjectDependency}
        .toSet()
        .collect{"$rootProject.projectDir/${it.name}.md"}

dokka {
    includes = includeFiles + "$rootProject.projectDir/${project.name}.md"
}

buildDir = "$rootProject.buildDir/$project.name"
mainClassName = "com.tormenteddan.demos.DemoKt"

run {
    if (project.hasProperty("demo")){
        args = [ "${demo}" ]
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile,
                'Main-Class': mainClassName
        )
    }
}